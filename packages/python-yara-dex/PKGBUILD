# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-yara-dex
_pkgname=yara-python
pkgver=1.0.3
pkgrel=1
pkgdesc='The Python interface for YARA.'
url='https://github.com/MobSF/yara-python-dex'
arch=('any')
groups=('blackarch')
license=('Apache')
makedepends=('python-setuptools' 'git' 'glibc' 'python' 'yara')
provides=('python-yara')
conflicts=('python-yara' 'python-yara-rednaga')
source=("git+https://github.com/MobSF/yara-python-dex.git")
sha512sums=('SKIP')

prepare() {
  cd "$_pkgname-dex"

  git submodule update --init --recursive
}

build() {
  cd "$_pkgname-dex"

  python setup.py build --enable-dex
}

package() {
  cd "$_pkgname-dex"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README"
}

