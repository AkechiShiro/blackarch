# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-yara-python-dex
#_gitpkgname=yara-python
_pkgname=yara-python-dex
pkgver=1.0.3.r0.g8e2df94
pkgrel=1
pkgdesc='The Python interface for YARA.'
arch=('any')
url='https://github.com/MobSF/yara-python-dex/releases'
license=('Apache')
makedepends=('git' 'python-setuptools' 'glibc' 'python' 'yara')
provides=('python-yara')
conflicts=('python-yara' 'python-yara-rednaga')
options=(!emptydirs)
source=("git+https://github.com/MobSF/${_pkgname}.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
 cd $_pkgname
 git submodule update --init --recursive
}

build() {
  cd "$_pkgname"

  python setup.py build
}

package() {
  cd "$_pkgname"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

